apply plugin: 'com.android.application'
ext {
    /* Version code for *next* release, bump *after* a release is created. */
    // 1 or more digits
    versionMajor = 1
    // exactly 2 digit
    versionMinor = 3
    // exactly 2 digits
    versionBuild = 4
    versionRC = 0
}
android {
    compileSdkVersion 30

    signingConfigs {
        pa760 {
            def ksFile = rootProject.file("./keystore/keystore.pa760.properties")
            def props = new Properties()
            if (ksFile.canRead()) {
                props.load(new FileInputStream(ksFile))

                if (props != null) {
                    storeFile file(props['storeFile'])
                    keyPassword props['keyPassword']
                    keyAlias props['keyAlias']
                    storePassword props['storePassword']
                    println "The key for device pa760 has been setup"
                } else {
                    println 'some entries in \'keystore.properties\' not found!'
                }
            } else {
                println '\'keystore.properties\' not found!'
            }
        }
        wd200 {
            def ksFile = rootProject.file("./keystore/keystore.wd200.properties")
            def props = new Properties()
            if (ksFile.canRead()) {
                props.load(new FileInputStream(ksFile))

                if (props != null) {
                    storeFile file(props['storeFile'])
                    keyPassword props['keyPassword']
                    keyAlias props['keyAlias']
                    storePassword props['storePassword']
                    println "The key for device wd200 has been setup"
                } else {
                    println 'some entries in \'keystore.properties\' not found!'
                }
            } else {
                println '\'keystore.properties\' not found!'
            }
        }
    }

    defaultConfig {
        applicationId "com.unitech.scanner.utility"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode versionMajor * 10000 + versionMinor * 100 + versionBuild
        if (versionRC > 0) {
            versionName "${versionMajor}.${versionMinor}.${versionBuild}-RC${versionRC}"
        } else {
            versionName "${versionMajor}.${versionMinor}.${versionBuild}"
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        system {
            signingConfig release.signingConfig
        }
        debug {
        }
    }
    flavorDimensions "device"

    productFlavors {
        pa760 {
            targetSdkVersion 29
            signingConfig signingConfigs.pa760
            android.sourceSets.pa760 {
                manifest {
                    srcFile 'src/main/system_manifest/AndroidManifest.xml'
                }
            }
        }
        wd200 {
            targetSdkVersion 29
            signingConfig signingConfigs.wd200
            android.sourceSets.wd200 {
                manifest {
                    srcFile 'src/main/system_manifest/AndroidManifest.xml'
                }
            }
        }
        defaultDevice {
            android.sourceSets.defaultDevice {
                manifest {
                    srcFile 'src/main/AndroidManifest.xml'
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation files('libs/UnitechSDK_1.1.4.jar')
    implementation files('libs/SppProtocol.jar')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation 'com.google.android.material:material:1.3.0'
    //implementation 'com.orhanobut:logger:2.2.0'
    implementation 'org.tinylog:tinylog-api:2.1.2'
    implementation 'org.tinylog:tinylog-impl:2.1.2'
    implementation('com.journeyapps:zxing-android-embedded:3.6.0') { transitive = false }
    implementation 'com.google.zxing:core:3.4.0'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'org.hashids:hashids:1.0.3'
    implementation 'com.nfx.android:range-bar-preference:0.0.6'

    implementation 'androidx.emoji:emoji-bundled:1.1.0'
    implementation 'androidx.emoji:emoji:1.1.0'
    implementation 'androidx.emoji:emoji-appcompat:1.1.0'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation 'androidx.preference:preference:1.1.1'

    implementation "com.fasterxml.jackson.core:jackson-databind:2.9.0"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.0"
}

allprojects {

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
        }
    }
}